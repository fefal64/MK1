# El Vampiro Ramiro y el misterio del Papiro
# Copyleft 2013, 2020 by The Mojon Twins.
# MTE MK1 4.8

# flags:
#  0 - All purpose no repeat
#  1 - Trap Sonia
#  2 - Trap Clemente
#  3 - Trap Marisa
#  4 - Trap Gustavo
#  5 -
#  6 - Open pyramid
#  9 - Lower floor door
# 10 - COIN_FLAG
# 11 - Trap Sonia done
# 12 - Trap Clemente done
# 13 - Trap Marisa done
# 14 - Trap Gustavo done
# 15 - Trap number
# 16 - SCORE
# 17 - Advertencia de Ramón (Ojo)
# 18 - Advertencia de Ramón (Agua)
# 19 - Altar de las ofrendas mal

ENTERING SCREEN 0
	IF TRUE
	THEN
		     #XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
		TEXT "       RAMON EL FARAON"
		SET FLAG 0 = 0
		SET TILE (2, 6) = 27
	END
END

PRESS_FIRE AT SCREEN 0
	IF PLAYER_TOUCHES 2, 6
	IF OBJECT_COUNT = 4
	THEN
		EXTERN 64
		WIN GAME
	END

	IF PLAYER_TOUCHES 2, 6
	IF FLAG 0 = 0
	THEN
		EXTERN 1
		SET FLAG 6 = 1
		SET FLAG 0 = 1
		INC FLAG 16, @SCORE_OPEN_PYRAMID
	END
END

## Trampa de Gustavo el Esclavo

ENTERING SCREEN 2
	IF TRUE 
	THEN
		SET TILE (9, 7) = 26
		     #XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
		TEXT "      GUSTAVO EL ESCLAVO"
	END

	IF FLAG 4 = 2
	THEN
		SET TILE (14, 1) = 0
		SET TILE (14, 2) = 0
	END
END

PRESS_FIRE AT SCREEN 2
	IF PLAYER_TOUCHES 9, 7
	THEN
		EXTERN 68
	END
END

ENTERING SCREEN 3
	IF TRUE
	THEN
		SET FLAG 15 = 4
	END

	IF FLAG 14 = 1 
	THEN
		SET TILE (1, 5) = 46
		SET TILE (2, 8) = 11
	END
END

ENTERING SCREEN 5
	IF TRUE
	THEN
		     #XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
		TEXT "         LA  CISTERNA"
		SET TILE (10, 8) = 27
	END
END

PRESS_FIRE AT SCREEN 5
	IF PLAYER_TOUCHES 10, 8
	THEN
		EXTERN 25
	END

	IF PLAYER_TOUCHES 10, 8
	IF FLAG 18 = 0
	THEN
		SET FLAG 18 = 1
		INC FLAG 16, @SCORE_RAMON_WATER
	END
END

## Altar roto

ENTERING SCREEN 6
	IF TRUE
	THEN
		SET TILE (3, 6) = 21
		SET TILE (4, 6) = 22	
	END
END 

PRESS_FIRE AT SCREEN 6
	IF PLAYER_IN_X_TILES 3, 4
	IF PLAYER_IN_Y_TILES 6, 6
	THEN
		EXTERN 3
	END
END

## Trampa de Marisa la Sacerdotisa

ENTERING SCREEN 10
	IF TRUE
	THEN
		SET TILE (7, 5) = 25
		     #XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
		TEXT "    MARISA LA SACERDOTISA"
	END

	IF FLAG 3 = 2
	THEN
		SET TILE (5, 7) = 0
		SET TILE (5, 8) = 0
	END
END

PRESS_FIRE AT SCREEN 10
	IF PLAYER_TOUCHES 7, 5
	THEN
		EXTERN 67
	END
END

ENTERING SCREEN 16
	IF TRUE
	THEN 
		SET FLAG 15 = 3
	END IF

	IF FLAG 13 = 1 
	THEN
		SET TILE (7, 2) = 46
		SET TILE (6, 5) = 46
		SET TILE (7, 8) = 11
	END
END

## Altar de las ofrendas

ENTERING SCREEN 13
	IF TRUE
	THEN
		     #XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
		TEXT "    ALTAR DE LAS OFRENDAS!"
		SET TILE (9, 8) = 21
		SET TILE (10, 8) = 22
	END 

	IF FLAG 6 = 1
	THEN
		SET TILE (3, 7) = 0
		SET TILE (3, 8) = 0
	END
END

PRESS_FIRE AT SCREEN 13
	IF PLAYER_IN_X_TILES 9, 10
	IF PLAYER_IN_Y_TILES 8, 8
	THEN
		EXTERN 128
	END
END

## Advertencia de Ramón

ENTERING SCREEN 14
	IF TRUE
	THEN
		SET TILE (2, 8) = 27
	END
END

PRESS_FIRE AT SCREEN 14
	IF PLAYER_TOUCHES 2, 8
	THEN
		EXTERN 32
	END

	IF PLAYER_TOUCHES 2, 8
	IF FLAG 17 = 0
	THEN
		SET FLAG 17 = 1
		INC FLAG 16, @SCORE_RAMON_EYE
	END
END

## Trampa de Clemente la Serpiente

ENTERING SCREEN 19
	IF TRUE
	THEN
		SET TILE (2, 2) = 24
		     #XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
		TEXT "    CLEMENTE LA SERPIENTE"
	END

	IF FLAG 2 = 2
	THEN
		SET TILE (0, 1) = 0
		SET TILE (0, 2) = 0
	END
END

PRESS_FIRE AT SCREEN 19
	IF PLAYER_TOUCHES 2, 2
	THEN
		EXTERN 66
	END
END

## Trampa de Sonia la Momia

ENTERING SCREEN 25
	IF TRUE
	THEN
		SET TILE (2, 2) = 23
			 #XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
		TEXT "       SONIA LA MOMIA"
	END

	IF FLAG 1 = 2
	THEN
		SET TILE (0, 1) = 0
		SET TILE (0, 2) = 0
	END
END

PRESS_FIRE AT SCREEN 25
	IF PLAYER_TOUCHES 2, 2
	THEN
		EXTERN 65
	END
END

## Lower floor gate

ENTERING SCREEN 30
	IF FLAG 9 = 0
	THEN
		SET TILE (3, 8) = 20
	END

	IF FLAG 9 = 1
	THEN
		SET TILE (3, 8) = 16
	END
END

PRESS_FIRE AT SCREEN 30
	IF PLAYER_TOUCHES 3, 8
	IF FLAG 9 = 0
	THEN
		SET FLAG 9 = 1
		SET TILE (3, 8) = 16
		SHOW
		SOUND 8
	END
END

ENTERING SCREEN 35
	IF FLAG 9 = 0
	THEN
		SET TILE (2, 7) = 10
	END
END

## Remove when finished...

ENTERING GAME
	IF TRUE
	THEN
	#	SET FLAG 6 = 1
	END
END
